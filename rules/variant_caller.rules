import os

localrules: call_variants

SV_TYPES = ("insertion", "deletion")
INDEL_TYPES = ("insertion", "deletion")
LOCAL_ASSEMBLY_ALIGNMENTS = config.get("local_assembly_alignments", "local_assembly_alignments.bam")
VARIANTS = config.get("variants", "variants.bed")
MIN_CONTIG_LENGTH = 40000

#
# All variant calls
#

# TODO: output VCF
rule call_variants:
    input: "sv_calls.bed", "indel_calls.bed", "sv_calls/merged_inversions.bed"
    output: VARIANTS
    params: sge_opts=""
    shell: "sort -k 1,1 -k 2,2n {input} > {output}"

#
# Small insertion/deletion (indel) calls
#

rule call_indels:
    input: expand("indel_calls/{indel_type}.tab", indel_type=INDEL_TYPES)
    output: "indel_calls.bed"
    params: sge_opts=""
    shell: "sort -k 1,1 -k 2,2n {input} > {output}"

rule combine_annotations_for_indel_type:
    input: "indel_calls/{indel_type}/filtered_gaps.bed", "indel_calls/{indel_type}/support_by_assembled_contigs.txt", "indel_calls/{indel_type}/assembled_contigs_coverage.txt", "indel_calls/{indel_type}/read_coverage.txt"
    output: "indel_calls/{indel_type}.tab"
    params: sge_opts=""
    shell: """paste {input} | awk 'OFS="\\t" {{ print $0,"{wildcards.indel_type}" }}' > {output}"""

rule annotate_support_from_assembled_contigs_for_indels:
    input: "indel_calls/{indel_type}/gaps_2bp_or_more_without_homopolymers.bed"
    output: "indel_calls/{indel_type}/support_by_assembled_contigs.txt"
    params: sge_opts=""
    shell: "cut -f 6 {input} > {output}"

rule annotate_strs_in_indels:
    input: "indel_calls/{indel_type}/gaps_2bp_or_more_without_homopolymers.bed", "strs_in_reference.bed"
    output: "indel_calls/{indel_type}/strs.txt"
    params: sge_opts=""
    shell: "cut -f 1-3 {input[0]} | bedtools intersect -a stdin -b {input[1]} -loj -sorted | sed 's/\\t\./\\t0/g' | bedtools groupby -c 7 -o first -full | cut -f 7 > {output}"

# TODO: confirm that this coverage.bed works like the original hg19 bed from Mark
rule annotate_coverage_of_pacbio_reads_for_indels:
    input: "indel_calls/{indel_type}/gaps_2bp_or_more_without_homopolymers.bed", "coverage.bed"
    output: "indel_calls/{indel_type}/read_coverage.txt"
    params: sge_opts=""
    shell: """cut -f 1-3 {input[0]} | bedtools intersect -a stdin -b {input[1]} -loj -sorted | sed 's/\\t\./\\t0/g' | bedtools groupby -c 7 -o mean -full | cut -f 8 | awk '{{ printf("%2.2f\\n", $1) }}' > {output}"""

rule annotate_coverage_of_assembled_contigs_for_indels:
    input: "indel_calls/{indel_type}/gaps_2bp_or_more_without_homopolymers.bed", "assembled_contigs.depth.bed"
    output: "indel_calls/{indel_type}/assembled_contigs_coverage.txt"
    params: sge_opts=""
    shell: "cut -f 1-3 {input[0]} | bedtools intersect -a stdin -b {input[1]} -loj -sorted | sed 's/\\t\./\\t0/g' | bedtools groupby -c 7 -o max -full | cut -f 8 > {output}"

rule calculate_coverage_from_assembled_contigs:
    input: reference=config["reference"], alignments=LOCAL_ASSEMBLY_ALIGNMENTS
    output: "assembled_contigs.depth.bed"
    params: sge_opts="-l mfree=12G"
    shell: "bedtools bamtobed -i {input.alignments} | {SNAKEMAKE_DIR}/scripts/BedIntervalsToDepth.py /dev/stdin {input.reference} --out /dev/stdout | bedtools sort > {output}"

rule cut_indel_events_by_columns:
    input: "indel_calls/{indel_type}/gaps_2bp_or_more_without_homopolymers.bed"
    output: "indel_calls/{indel_type}/filtered_gaps.bed"
    params: sge_opts=""
    shell: "cut -f 1-5 {input} > {output}"

rule filter_indel_events_by_size:
    input: "indel_calls/{indel_type}/gaps_without_homopolymers.bed"
    output: "indel_calls/{indel_type}/gaps_2bp_or_more_without_homopolymers.bed"
    params: sge_opts=""
    shell: "awk '$4 >= 2' {input} | bedtools groupby -c 6 -o max -full > {output}"

rule remove_indel_events_in_homopolymers:
    input: "indel_calls/{indel_type}/gaps.bed"
    output: "indel_calls/{indel_type}/gaps_without_homopolymers.bed"
    params: sge_opts=""
    run:
        command = """awk '$11 == "F"' {input} | cut -f 1,2,3,5,6,8 | sort -k 1,1 -k 2,2n -k 4,4n | {SNAKEMAKE_DIR}/scripts/PrintSNVSupport.py /dev/stdin /dev/stdout"""
        if wildcards.indel_type == "insertion":
            command = "%s | {SNAKEMAKE_DIR}/scripts/BedMod.py /dev/stdin {output} --leftjustify 1" % command
        else:
            command = "%s > {output}" % command

        shell(command)

rule split_indels_by_type:
    input: "indel_calls/gaps.tiled.bed"
    output: "indel_calls/{indel_type}/gaps.bed"
    params: sge_opts=""
    shell: "grep {wildcards.indel_type} {input} | bedtools sort -i stdin > {output}"

rule filter_indel_gaps_by_tiling_path:
    input: "indel_calls/gaps.bed", "indel_calls/tiling_contigs.tab"
    output: "indel_calls/gaps.tiled.bed", "indel_calls/gaps.tiled.log"
    params: sge_opts="-l mfree=4G"
    shell: "{SNAKEMAKE_DIR}/scripts/FilterGapsByTilingPath.py {input} > {output[0]} 2> {output[1]}"

rule tile_contigs_from_alignments:
    input: LOCAL_ASSEMBLY_ALIGNMENTS
    output: "indel_calls/tiling_contigs.tab"
    params: sge_opts=""
    shell: "samtools view -h {input} | {SNAKEMAKE_DIR}/scripts/TilingPath.py /dev/stdin > {output}"

rule find_indel_gaps_in_alignments:
    input: reference=config["reference"], alignments=LOCAL_ASSEMBLY_ALIGNMENTS
    output: "indel_calls/gaps.bed"
    params: sge_opts="", indel_pack_distance="0", min_contig_length=str(MIN_CONTIG_LENGTH)
    shell: "samtools view {input.alignments} | {SNAKEMAKE_DIR}/scripts/PrintGaps.py {input.reference} /dev/stdin --minLength 0 --maxLength 50 --context 6 --removeAdjacentIndels --onTarget --minContigLength {params.min_contig_length} --condense {params.indel_pack_distance} --outFile {output}"


#
# Structural variation (SV) calls
#

rule call_svs:
    input: "sv_calls.bed"
    params: sge_opts=""

rule collect_all_summarized_sv_calls:
    input: expand("sv_calls/repeat_classified_{sv_type}.bed", sv_type=SV_TYPES)
    output: "sv_calls/sv_calls_with_repeats.bed"
    params: sge_opts=""
    shell: "sort -k 1,1 -k 2,2n -m {input} > {output}"

rule collect_summarized_sv_calls_within_type:
    input: "sv_calls/summarized_{sv_type}"
    output: "sv_calls/repeat_classified_{sv_type}.bed"
    params: sge_opts=""
    shell: """for file in {input}/*.bed; do repeat_type=`basename ${{file/.bed/}} | sed 's/\./_/g'`; awk -v repeat_type=$repeat_type 'OFS="\\t" {{ print $0,repeat_type }}' $file; done | sort -k 1,1 -k 2,2n > {output}"""

rule summarize_calls_by_repeat_type:
    input: "sv_calls/all_annotated_with_trf.{sv_type}.bed"
    output: "sv_calls/summarized_{sv_type}"
    params: sge_opts=""
    shell:
        "mkdir -p {output}; "
        "awk '$20 > 0.8' {input} > {output}/TRF.bed; "
        "awk '$20 <= 0.8' {input} > sv_calls/not_trf.bed; "
        "{SNAKEMAKE_DIR}/scripts/FixMasked.py 17 < sv_calls/not_trf.bed | awk '$18 < 0.7' > {output}/NotMasked.bed; "
        "{SNAKEMAKE_DIR}/scripts/FixMasked.py 17 < sv_calls/not_trf.bed | awk '$18 >= 0.7' > sv_calls/repeat.bed; "

        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py sv_calls/repeat.bed --prefix AluY --minPrefix 1 --maxPrefix 1 --maxNotPrefix 0 --maxSTR 0 --remainder {output}/1.bed > {output}/AluY.simple.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/1.bed --prefix AluS --minPrefix 1 --maxPrefix 1 --maxNotPrefix 0 --maxSTR 0 --remainder {output}/2.bed > {output}/AluS.simple.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/2.bed --minSTR 1  --maxNotPrefix 0 --remainder {output}/4.bed > {output}/STR.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/4.bed --prefix L1HS  --maxNotPrefix 0 --remainder {output}/5.bed > {output}/L1HS.simple.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/5.bed --prefix Alu  --minPrefix 1 --maxNotPrefix 0 --maxSTR 0 --remainder {output}/6.bed > {output}/Alu.Mosaic.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/6.bed --prefix Alu  --minSTR 1 --minPrefix 1 --maxNotPrefix 0 --remainder {output}/7.bed > {output}/Alu.STR.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/7.bed --prefix ALR   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/8.bed > {output}/ALR.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/8.bed --prefix SVA   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/9.bed > {output}/SVA.simple.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/9.bed --prefix HERV   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/10.bed > {output}/HERV.simple.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/10.bed --prefix L1P   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/11.bed > {output}/L1P.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/11.bed --prefix BSR/Beta   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/12.bed > {output}/Beta.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/12.bed --prefix HSAT   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/13.bed > {output}/HSAT.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/13.bed --prefix MER   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/14.bed > {output}/MER.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/14.bed --prefix L1   --minPrefix 1 --maxNotPrefix 0 --remainder {output}/15.bed > {output}/L1.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/15.bed --prefix LTR  --minPrefix 1 --maxNotPrefix 0 --remainder {output}/16.bed > {output}/LTR.bed; "
        "{SNAKEMAKE_DIR}/scripts/PrintUniqueEvents.py {output}/16.bed --max 1 --remainder {output}/17.bed > {output}/Singletons.bed; "
        "mv -f {output}/17.bed {output}/Complex.bed; "
        "rm -f {output}/[0-9]*.bed"

rule merge_sv_calls:
    input: expand("sv_calls/all_annotated.{sv_type}.bed", sv_type=SV_TYPES)
    output: "sv_calls.bed"
    params: sge_opts=""
    shell: "sort -k 1,1 -k 2,2n {input} > {output}"

rule filter_annotated_sv_calls:
    input: "sv_calls/all_annotated.{sv_type}.bed"
    output: "sv_calls/annotated.{sv_type}.bed"
    params: sge_opts=""
    # Filter out non-repetitive SVs or SVs with less than 70% masked sequence.
    shell: "grep -v NONE {input} | awk '$18 >= 0.70' > {output}"

rule annotate_sv_calls_with_trf:
    input: "sv_calls/all_annotated.{sv_type}.bed", "sv_calls/{sv_type}/rm/{sv_type}.fasta.trf"
    output: "sv_calls/all_annotated_with_trf.{sv_type}.bed"
    params: sge_opts=""
    shell: "{SNAKEMAKE_DIR}/scripts/AnnotateWithTRF.py {input} {output}"

rule annotate_sv_calls_with_repeatmasker:
    input: calls="sv_calls/calls.{sv_type}.bed", repeats="sv_calls/{sv_type}/rm/{sv_type}.fasta.out", masked_fasta="sv_calls/{sv_type}/rm/{sv_type}.fasta.masked"
    output: "sv_calls/all_annotated.{sv_type}.bed"
    params: sge_opts=""
    shell: "{SNAKEMAKE_DIR}/scripts/AnnotateGapBed.py {input.calls} {output} {input.repeats} {input.masked_fasta}"

rule trf_mask_sv_fasta:
    input: "sv_calls/{sv_type}/{sv_type}.fasta"
    output: "sv_calls/{sv_type}/rm/{sv_type}.fasta.trf"
    params: sge_opts="-pe serial 1 -l mfree=2G"
    shell: "{SNAKEMAKE_DIR}/bin/trf {input} 2 7 7 80 10 20 500 -m -ngs -h > {output}"

rule repeatmask_sv_fasta:
    input: "sv_calls/{sv_type}/{sv_type}.fasta"
    output: "sv_calls/{sv_type}/rm/{sv_type}.fasta.out", "sv_calls/{sv_type}/rm/{sv_type}.fasta.masked"
    params: sge_opts="-pe serial 8 -l mfree=1G", threads="8"
    shell: "RepeatMasker -dir `dirname {output[0]}` -xsmall -no_is -e wublast -s -pa {params.threads} {input}"

rule create_sv_fasta:
    input: "sv_calls/calls.{sv_type}.bed"
    output: "sv_calls/{sv_type}/{sv_type}.fasta"
    params: sge_opts=""
    shell: "{SNAKEMAKE_DIR}/scripts/GapBedToFasta.py {input} {output}"

def _get_call_comparison_action(wildcards):
    if wildcards.sv_type == "insertion":
        return "window"
    else:
        return "intersect"

rule identify_calls_by_type:
    input: gaps="sv_calls/gaps.bed", alignments=LOCAL_ASSEMBLY_ALIGNMENTS
    output: "sv_calls/calls.{sv_type}.bed"
    params: sge_opts="", call_comparison_action=_get_call_comparison_action, window="20", overlap="0.5"
    # Filter input gaps by SV type and omit gaps containing N bases. Insertion
    # coordinates are resized to their single-bp representation prior to
    # clustering and returned to their original values after clustering. After
    # clustering each call is annotated by the coverage of local assemblies.
    shell: """awk '$4 == "{wildcards.sv_type}" && index($6, "N") == 0' {input.gaps} | awk 'OFS="\\t" {{ if ("{wildcards.sv_type}" == "insertion") {{ $3=$2 + 1 }} print }}' | python {SNAKEMAKE_DIR}/scripts/cluster_calls.py --window {params.window} --reciprocal_overlap {params.overlap} /dev/stdin {params.call_comparison_action} | awk 'OFS="\\t" {{ if ("{wildcards.sv_type}" == "insertion") {{ $3=$2 + $5 }} print }}' | sort -k 1,1 -k 2,2n | while read line; do set -- $line; coverage=`samtools view -c {input.alignments} $1:$2-$3`; echo -e "$line\\t$coverage"; done > {output}"""

rule merge_inversions:
    input: inversions="sv_calls/inversions.bed", alignments=LOCAL_ASSEMBLY_ALIGNMENTS
    output: "sv_calls/merged_inversions.bed"
    params: sge_opts=""
    run:
        # Only try to sort and merge inversions if any exist.
        if os.stat(input.inversions).st_size > 0:
            shell("""sort -k 1,1 -k 2,2n {input.inversions} | bedtools merge -i stdin -d 0 -c 4 -o count | while read line; do set -- $line; coverage=`samtools view -c {input.alignments} $1:$2-$3`; echo -e "$line\\t$coverage"; done > {output}""")
        else:
            shell("touch {output}")

rule find_inversions:
    input: alignments=LOCAL_ASSEMBLY_ALIGNMENTS, reference=config["reference"]
    output: "sv_calls/inversions.bed"
    params: sge_opts="-pe serial 8 -l mfree=2G -l h_rt=03:00:00", reference_window="5000", threads="8"
    shell: "samtools view {input.alignments} | {SNAKEMAKE_DIR}/scripts/mcst/screenInversions /dev/stdin {input.reference} {output} -w {params.reference_window} -r --noClip -j {params.threads}"

rule find_calls_by_gaps_in_alignments:
    input: reference=config["reference"], alignments=LOCAL_ASSEMBLY_ALIGNMENTS
    output: "sv_calls/gaps.bed"
    params: sge_opts="", tsd_length="20", indel_pack_distance="20"
    shell: "samtools view -h {input.alignments} | {SNAKEMAKE_DIR}/scripts/PrintGaps.py {input.reference} /dev/stdin --qpos --condense {params.indel_pack_distance} --tsd {params.tsd_length} --outFile {output}"
